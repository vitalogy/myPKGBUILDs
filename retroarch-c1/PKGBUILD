# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Themaister <maister@archlinux.us>
# Contributor: lifning <definelightning@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - enable GLES

pkgname=retroarch-c1
pkgver=1.3.6
pkgrel=1
pkgdesc='Reference frontend for the libretro API'
arch=('armv7h')
url='http://www.libretro.com/'
license=('GPL')
groups=('libretro')
depends=('alsa-lib' 'gcc-libs' 'glibc' 'libdrm' 'libgl' 'libpulse' 'libusb'
         'libx11' 'libxcb' 'libxext' 'libxinerama' 'libxkbcommon' 'libxv'
         'libxxf86vm' 'mesa' 'openal' 'sdl2' 'wayland' 'zlib'
         'libass.so' 'libavcodec.so' 'libavformat.so' 'libavutil.so'
         'libfreetype.so' 'libswresample.so' 'libswscale.so' 'libudev.so'
         'odroid-c1-libgl-fb' 'retroarch-assets-xmb')
makedepends=('git' 'vulkan-icd-loader')
optdepends=('libretro-desmume: Nintendo DS core'
            'libretro-gambatte: Nintendo Game Boy/Game Boy Color core'
            'libretro-genesis-plus: Sega Master System/Genesis/Game Gear/CD/32X core'
            'libretro-mgba: Nintendo Game Boy Advance core'
            'libretro-mupen64plus: Nintendo 64 core'
            'libretro-nestopia: Nintendo Entertainment System core'
            'libretro-pcsx-rearmed: Sony PlayStation core'
            'libretro-reicast: Sega Dreamcast core'
            'libretro-snes9x: Super Nintendo Entertainment System core'
            'libretro-yabause: Sega Saturn core'
            'libretro-overlays: Collection of overlays'
            'libretro-shaders: Collection of shaders'
            'retroarch-assets-xmb: XMB menu assets'
            'retroarch-autoconfig-udev: udev joypad autoconfig')
backup=('etc/retroarch.cfg')
source=("git+https://github.com/libretro/RetroArch.git#tag=v${pkgver}"
        'git+https://github.com/KhronosGroup/glslang.git#commit=a4a4d5e'
        'git+https://github.com/KhronosGroup/SPIRV-Cross.git#commit=5c24d99'
        'mali_fbdev_ctx.patch'
        'retroarch.cfg'
        'retroarch.service')
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'e287e403f539f4d797b2a98c4c6c8027a5bf004436357d17c3b911b5a63549cc'
            '11f6246341285686dab01f52a5719e415f9eb5a56cac76498b096a64631d304b'
            'c66696a67e016a21606a51e75e59d224eeba3ad680b19e23379f50fb3a02ef8f')


prepare() {
  cd RetroArch

  git submodule init deps/{glslang/glslang,SPIRV-Cross}
  git config submodule.glslang.url ../glslang
  git config submodule.SPIRV-Cross.url ../SPIRV-Cross
  git submodule update deps/{glslang/glslang,SPIRV-Cross}

  patch -Np1 -i ${srcdir}/mali_fbdev_ctx.patch
}

build() {
  cd RetroArch

  ./configure \
    --prefix='/usr' \
    --disable-cg \
    --disable-jack \
    --disable-oss \
    --disable-kms \
    --disable-x11 \
    --disable-xvideo \
    --disable-vg \
    --enable-xmb \
    --enable-gles \
    --enable-mali_fbdev \
    --enable-neon \
    --enable-floathard \
    --enable-fbo \
    --enable-zlib \
    --enable-freetype
  make
  make -C gfx/video_filters
  make -C audio/audio_filters
}

package() {
  install="retroarch.install"
  provides=('retroarch')
  conflicts=('retroarch')
  cd RetroArch

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/lib/retroarch/filters/{audio,video}
  install -m 644 gfx/video_filters/*.{filt,so} "${pkgdir}"/usr/lib/retroarch/filters/video/
  install -m 644 audio/audio_filters/*.{dsp,so} "${pkgdir}"/usr/lib/retroarch/filters/audio/

  install -Dm0644 $srcdir/retroarch.service $pkgdir/usr/lib/systemd/system/retroarch.service
  install -m 644 $srcdir/retroarch.cfg $pkgdir/etc/retroarch.cfg
}
